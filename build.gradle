plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    // 1. 문서 만들기
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'shop.mtcoding'
version = '1.0'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 2. 문서만들기
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation group: 'com.auth0', name: 'java-jwt', version: '4.3.0'
    implementation group: 'org.qlrm', name: 'qlrm', version: '4.0.1'
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
// 3. 문서만들기
// 1. gradlew build (test코드 실행 -> 결과 build/generated-snippets 에 copy한다. *.adoc) - 변수 설정 완료
ext {
    set('snippetsDir', file("build/generated-snippets"))
}

// 2. gradlew build (test코드 실행 -> 진짜로 1번의 결과가 실횅됨)
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// 3. 테스트가 종료되면 asciidoctor 실행 -> *.adoc 파일들을 가지고 하나의 .adoc 파일로 변경 (src/docs/asciidoc)
tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

// 4. HTML 파일을 서버에서 열어볼 수 있게 Copy
tasks.register('copyRestDocs', Copy) {
    dependsOn tasks.named('asciidoctor')
    from "${asciidoctor.outputDir}"  // asciidoctor 결과 위치
    into "src/main/resources/static/docs"
}

tasks.named('bootJar') {
    dependsOn tasks.named('copyRestDocs')
}

// plain 파일 생성하지 않기
jar {
    enabled = false
}